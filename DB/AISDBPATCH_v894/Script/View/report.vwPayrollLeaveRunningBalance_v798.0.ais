 --=============================================
 --Author:		Abdiel Corda
 --Create date: 08/14/2018
 --Description:	Earning/Deduction with Allowance Details 
 --That displays on payslip templates
 --=============================================

ALTER VIEW [report].[vwPayrollLeaveRunningBalance]
--THIS WILL BE USED TO DISPLAY AVAILABLE LEAVE CREDITS IN PAYSLIP
AS

SELECT 
	ROWID = 0 /*ROW_NUMBER() OVER (ORDER BY  LTS.Code,LBL.TransactionDate DESC)*/
	,PostedDate = LBL.TransactionDate
	,EP.ProfileId
	,LeaveTypeCode = LTS.Code
	,LBDStatus = 0
	,EarnedHours = 
	(
		CAST((CASE WHEN ISNULL(COMPCONFIG.DisplayInDays,0) = 1 AND ISNULL(LBL.PreviousBalance,0) <> 0 THEN ISNULL(LBL.PreviousBalance / COMPCONFIG.NoOfDays,0) ELSE ISNULL(LBL.PreviousBalance,0) END) AS DECIMAL(18,4))
	)
	,UsedHours =
	(
		CAST((CASE WHEN ISNULL(COMPCONFIG.DisplayInDays,0) = 1 AND ISNULL(LBL.UsedHours,0) <> 0 THEN ISNULL(LBL.UsedHours / COMPCONFIG.NoOfDays,0) ELSE ISNULL( LBL.UsedHours,0) END) AS DECIMAL(18,4))
	)
	,CurrentBalance =
	(
		CAST((CASE WHEN ISNULL(COMPCONFIG.DisplayInDays,0) = 1 AND ISNULL(LBL.CurrentBalance,0) <> 0 THEN ISNULL(LBL.CurrentBalance / COMPCONFIG.NoOfDays,0) ELSE ISNULL(LBL.CurrentBalance,0) END) AS DECIMAL(18,4))
	)
	,LeaveInDays =	
	(
		CASE WHEN ISNULL(LBL.CurrentBalance,0) <> 0 THEN FLOOR(LBL.CurrentBalance / COALESCE(EmpPolicy.NoOfWorkingHoursperDay,COMPCONFIG.NoOfDays,1)) ELSE 0 END
	)
	,LeaveInHours = 
	(
		CASE WHEN ISNULL(LBL.CurrentBalance,0) <> 0 THEN FLOOR((LBL.CurrentBalance % COALESCE(EmpPolicy.NoOfWorkingHoursperDay,COMPCONFIG.NoOfDays,1))) ELSE 0 END
	)
	,LeaveInMins =	
	(
		(CASE WHEN ISNULL(LBL.CurrentBalance,0) <> 0 THEN (((LBL.CurrentBalance % COALESCE(EmpPolicy.NoOfWorkingHoursperDay,COMPCONFIG.NoOfDays,1)) * 60) % 60) ELSE 0 END)
	)
	,NoOfWorkingHoursperDay = COALESCE(EmpPolicy.NoOfWorkingHoursperDay,COMPCONFIG.NoOfDays,1)
	,LBL.LeaveTypeSetupId
	,PHD.PaySheetHeaderDetailId
	,PHD.PaySheetHeaderId
FROM employee.Profile EP WITH (NOLOCK)
LEFT JOIN 
(
	SELECT 
		PHD.ProfileId
		,PH.CutOffStartDate
		,PH.CutOffEndDate
		,PHD.PaySheetHeaderDetailId
		,PH.PaySheetHeaderId
	FROM payroll.PaySheetHeaderDetail PHD WITH (NOLOCK)
	INNER JOIN payroll.PaySheetHeader PH WITH (NOLOCK) ON PH.PaySheetHeaderId = PHD.PaySheetHeaderId
	WHERE PH.StatusId = 2
)PHD ON PHD.ProfileId = EP.ProfileId

OUTER APPLY
(
	SELECT 
		T.* 
	FROM leave.LeaveBalanceHeader LBH
	JOIN leave.LeaveTypeSetup LT ON LT.LeaveTypeSetupId = LBH.LeaveTypeSetupId
	OUTER APPLY
	(
		SELECT TOP 1 
			PreviousBalance = LBL.PreviousBalance
			,UsedHours = LBL.UsedHours
			,CurrentBalance = LBL.CurrentBalance
			,TransactionDate = LBL.TransactionDate
			,LeaveTypeSetupId = LBH.LeaveTypeSetupId
		FROM report.fnRunningLeaveBalance(EP.ProfileId,PHD.CutOffStartDate,PHD.CutOffEndDate,LT.LeaveTypeSetUpId)LBL
		WHERE 1=1
		AND (LBH.ProfileId = LBL.ProfileId) 
		AND (LBL.LeaveTypeSetUpId = LT.LeaveTypeSetUpId)
	)T
	WHERE LBH.ProfileId = EP.ProfileId
	/*
	SELECT TOP 1
		LBL.* 
	FROM report.fnRunningLeaveBalance(EP.ProfileId,PHD.CutOffStartDate,PHD.CutOffEndDate)LBL
	WHERE LBL.ProfileId = EP.ProfileId
	*/
)LBL
/**INNER JOIN report.fnRunningLeaveBalance(0) LBL ON LBL.ProfileId = EP.ProfileId**/
INNER JOIN leave.LeaveTypeSetup LTS ON LTS.LeaveTypeSetupId = LBL.LeaveTypeSetUpId
/*EMPLOYEE POLICY*/
OUTER APPLY
(
	SELECT TOP 1 
		EmpPolicy.PropertyName
		,NoOfWorkingHoursperDay = (CASE WHEN ISNULL(CAST(EmpPolicy.Value AS DECIMAL(18,4)),0) > 0 THEN CAST(EmpPolicy.Value AS DECIMAL(18,4)) ELSE 1 END)
	FROM dbo.fnRetrieveEmployeePolicy(EP.ProfileId,1,0) EmpPolicy
	WHERE EmpPolicy.PropertyName IN ('NoOfWorkingHoursperDay')
)EmpPolicy


OUTER APPLY 
(
    SELECT TOP 1
	   CompanyConfig.CompanyId
	   ,DisplayInDays.DisplayInDays
	   /*,NoOfDays.NoOfDays*/
	   ,NoOfDays = (CASE WHEN ISNULL(CAST(NoOfDays.NoOfDays AS DECIMAL(18,4)),0) > 0 THEN CAST(NoOfDays.NoOfDays AS DECIMAL(18,4)) ELSE 1 END)
    FROM maintenance.CompanyConfig CompanyConfig

    OUTER APPLY
    (
	   SELECT
	   DisplayInDays = REPLACE(value,'1:','') 
	   FROM dbo.FN_SPLITWITHID(CompanyConfig.Value, '|') T
	   WHERE T.ROWID = 1
    )DisplayInDays

    OUTER APPLY
    (
	   SELECT
	   NoOfDays = REPLACE(value,'2:','') 
	   FROM dbo.FN_SPLITWITHID(CompanyConfig.Value, '|') T
	   WHERE T.ROWID = 2
    )NoOfDays

    WHERE CompanyConfig.CompanyId = LTS.CompanyId
    AND (CompanyConfig.Type = 'LVEConfiguration')

) COMPCONFIG

WHERE 1=1  
AND (CAST(LBL.TransactionDate AS DATE) > CAST('1/1/1900' AS DATE))

/*
CDC 3031
AND PHD.PaySheetHeaderDetailId = 22679
10688
*/


GO


